plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.9'
    id "io.freefair.lombok" version "6.6-rc1"
    id "io.spring.dependency-management" version "1.1.0"
}

idea {
    project {
        languageLevel = 11
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

def junitVersion = "5.9.0"

allprojects {
    group "org.kivilev"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            dependency "org.junit.jupiter:junit-jupiter-api:$junitVersion"
            dependency "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        }
    }

    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '10.3.4'
        configFile file("$project.rootDir/checkstyle.xml")
        ignoreFailures = false
        maxWarnings = 0
        maxErrors = 0
    }

    apply plugin: 'com.github.spotbugs'
    spotbugs {
        showProgress = false
        effort = 'max'
        reportLevel = 'low'
        excludeFilter = file("$project.rootDir/spotbugs-exclude.xml")
    }

    apply plugin: 'project-report'
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile).configureEach {
    options.with {
        encoding = 'UTF-8'
    }
}

/*
bootJar {
    enabled = false
}*/
